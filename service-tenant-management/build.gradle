description = 'SiteWhere Tenant Management Microservice'

dependencies {
	compile project(':sitewhere-core')
	compile project(':sitewhere-mongodb')
	compile project(':sitewhere-microservice')
	
	// HBase not currently supported.
	compileOnly project(':sitewhere-hbase')
}

apply plugin: 'org.springframework.boot'
springBoot {
    mainClassName = 'com.sitewhere.tenant.TenantManagementApplication'
}

// Keep original jar.
jar { enabled = true }

// Reclassify Spring Boot jar.
bootJar { classifier = 'boot' }

// Only publish thin jar.
apply plugin: 'maven-publish'
publishing {
    publications {
	mavenJava(MavenPublication) { from components.java }
    }
}

// Create a Dockerfile.
task dockerFile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile, dependsOn: bootJar) {
    destFile = project.file('build/docker/Dockerfile')
    from "${rootProject.ext['docker.base.image']}"
    maintainer "${rootProject.ext['docker.maintainer']}"
	
	// Copy Spring Boot jar.
	copyFile("${project.name}-${project.version}-boot.jar", "/")
	
	// Copy tenant templates.
	copyFile("templates", "/templates")

	if(project.hasProperty("debug")) {
		println "Generating DEBUG IMAGE for project ${project.name}"
		exposePort 8001
	}

	// Run Java command to start application.
	if(!project.hasProperty("debug")) {
		defaultCommand 'java', '-Xmx768M', '-XX:+UnlockExperimentalVMOptions', '-XX:+UseCGroupMemoryLimitForHeap', '-jar', "/${project.name}-${project.version}-boot.jar"
	} else {
		defaultCommand 'java', '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8001', '-Xmx768M', '-XX:+UnlockExperimentalVMOptions', '-XX:+UseCGroupMemoryLimitForHeap', '-jar', "/${project.name}-${project.version}-boot.jar"
	}
}

// Copy artifacts to Docker input folder.
task copyArtifactsToDocker(type: Copy, dependsOn: dockerFile) {
    from "${buildDir}/libs/${project.name}-${project.version}-boot.jar"
	from "${projectDir}/dockerimage" 
	into 'build/docker'
}

// Build an image from the Dockerfile.
task dockerImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: copyArtifactsToDocker) {
    inputDir = project.file('build/docker')
 	if(!project.hasProperty("debug")) {
	    tag = "${dockerRepository}/sitewhere/${project.name}:${version}"
	} else {
	    tag = "${dockerRepository}/sitewhere/${project.name}:debug-${version}"		
	}
}

// Push image to remote repository.
task dockerPush(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage, dependsOn: dockerImage) {
    imageName = "${dockerRepository}/sitewhere/${project.name}"
    tag = "${version}"
}
