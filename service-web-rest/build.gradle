description = 'SiteWhere Web/REST Microservice'

apply plugin: 'war'

apply plugin: 'org.springframework.boot'
springBoot {
    mainClass = 'com.sitewhere.web.microservice.WebRestApplication'
}

// Prevents Logback conflict.
configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

dependencies {
	compile project(':sitewhere-core')
	compile project(':sitewhere-spring-spi')
	compile project(':sitewhere-protobuf')
	compile project(':sitewhere-microservice')
	compile project(':sitewhere-grpc-model')
	
	compile ('org.springframework.boot:spring-boot-starter-web')
    compile group: 'org.springframework', name: 'spring-web'
	
	// Swagger
    compile group: 'io.swagger', name: 'swagger-annotations', version:'1.5.10'	
    compile group: 'io.springfox', name: 'springfox-swagger2', version:'2.7.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version:'2.7.0'
    
    // Spring Security
    compile group: 'org.springframework.security', name: 'spring-security-config'
    compile group: 'org.springframework.security', name: 'spring-security-core'
    compile group: 'org.springframework.security', name: 'spring-security-web'
	
	// Quartz.
    compile group: 'org.quartz-scheduler', name: 'quartz', version:'2.2.2'
}

// Create a Dockerfile.
task dockerFile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile, dependsOn: bootRepackage) {
    destFile = project.file('build/docker/Dockerfile')
    from "${rootProject.ext['docker.base.image']}"
    maintainer "${rootProject.ext['docker.maintainer']}"
	
	// Copy Spring Boot jar.
	copyFile("${project.name}-${project.version}.war", "/")

	// Run the startup script and start tailing the log.
	defaultCommand 'java', '-jar', "/${project.name}-${project.version}.war"
}

// Copy artifact to Docker input folder.
task copyArtifactToDocker(type: Copy, dependsOn: dockerFile) {
    from "${buildDir}/libs/${project.name}-${project.version}.war"
    into 'build/docker'
}

// Build an image from the Dockerfile.
task dockerImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: copyArtifactToDocker) {
    inputDir = project.file('build/docker')
    tag = "${dockerRepository}/sitewhere/${project.name}:${version}"
}
