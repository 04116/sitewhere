description = 'SiteWhere Command Destinations Microservice'

dependencies {
	compile project(':sitewhere-communication')
	compile project(':sitewhere-spring')
	compile project(':sitewhere-protobuf')
	compile project(':sitewhere-microservice')
	
	// Twilio dependencies.
    compile group: 'com.twilio.sdk', name: 'twilio-java-sdk', version:'3.4.5'
}

apply plugin: 'org.springframework.boot'
springBoot {
    mainClass = 'com.sitewhere.destinations.CommandDestinationsApplication'
}

// Create a Dockerfile.
task dockerFile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile, dependsOn: bootRepackage) {
    destFile = project.file('build/docker/Dockerfile')
    from "${rootProject.ext['docker.base.image']}"
    maintainer "${rootProject.ext['docker.maintainer']}"
	
	// Expose ports.
	exposePort 8080
	
	// Copy Spring Boot jar.
	copyFile("${project.name}-${project.version}.jar", "/")

	// Run Java command to start application.
	defaultCommand 'java', "-Xmx${rootProject.ext['docker.java.memory']}", '-jar', "/${project.name}-${project.version}.jar"
}

// Copy artifact to Docker input folder.
task copyArtifactToDocker(type: Copy, dependsOn: dockerFile) {
    from "${buildDir}/libs/${project.name}-${project.version}.jar"
    into 'build/docker'
}

// Build an image from the Dockerfile.
task dockerImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: copyArtifactToDocker) {
    inputDir = project.file('build/docker')
    tag = "${dockerRepository}/sitewhere/${project.name}:${version}"
}
