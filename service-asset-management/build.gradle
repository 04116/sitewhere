description = 'SiteWhere Asset Management Microservice'

dependencies {
	compile project(':sitewhere-core')
	compile project(':sitewhere-mongodb')
	compile project(':sitewhere-microservice')
	compile project(':sitewhere-grpc-asset-management')
	
	// Connectivity to other microservices.
	compile project(':sitewhere-grpc-device-management')
	
	// HBase not currently supported.
	compileOnly project(':sitewhere-hbase')
	
	// REST template for SCIM access.
    compile group: 'org.springframework', name: 'spring-web'
    compile group: 'org.apache.httpcomponents', name: 'httpclient'
	
	// WSO2 dependencies.
    compile group: 'com.google.code.ksoap2-android', name: 'ksoap2-android', version:'3.4.0'
    compile group: 'com.google.code.ksoap2-android', name: 'ksoap2-j2se', version:'3.4.0'
    compile group: 'com.google.code.ksoap2-android', name: 'ksoap2-extras', version:'3.4.0'
}

license {
	exclude "**/modules/wso2/**"
}

apply plugin: 'org.springframework.boot'
springBoot {
    mainClassName = 'com.sitewhere.asset.AssetManagementApplication'
}

// Keep original jar.
jar {
	enabled = true
}

// Reclassify Spring Boot jar.
bootJar {
	classifier = 'boot'
}

// Only publish thin jar.
apply plugin: 'maven-publish'
publishing {
	publications {
        mavenJava(MavenPublication) {
            from components.java
        }
	}
}

// Create a Dockerfile.
task dockerFile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile, dependsOn: bootJar) {
    destFile = project.file('build/docker/Dockerfile')
    from "${rootProject.ext['docker.base.image']}"
    maintainer "${rootProject.ext['docker.maintainer']}"
		
	// Expose ports.
	exposePort 8080
	if(project.hasProperty("debug")) {
		println "Generating DEGUB IMAGE for project ${project.name}"
		exposePort 8081
	}

	// Copy Spring Boot jar.
	copyFile("${project.name}-${project.version}-boot.jar", "/")

	// Run Java command to start application.
	if(!project.hasProperty("debug")) {
		defaultCommand 'java', '-server', '-Xmx768M', '-XX:+UnlockExperimentalVMOptions', '-XX:+UseCGroupMemoryLimitForHeap', '-jar', "/${project.name}-${project.version}-boot.jar"
	} else {
		defaultCommand 'java', '-server', '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8001', '-Djava.rmi.server.logCalls', '-Dsun.rmi.server.logLevel=FINER', '-Dsun.rmi.transport.logLevel=FINER', '-Djava.rmi.server.hostname=192.168.171.129', '-Dcom.sun.management.jmxremote.port=1099', '-Dcom.sun.management.jmxremote.rmi.port=1106', '-Dcom.sun.management.jmxremote.authenticate=false', '-Dcom.sun.management.jmxremote.ssl=false', '-Dcom.sun.management.jmxremote.local.only=false', '-Xmx768M', '-XX:+UnlockExperimentalVMOptions', '-XX:+UseCGroupMemoryLimitForHeap', '-jar', "/${project.name}-${project.version}-boot.jar"
	}
}

// Copy artifact to Docker input folder.
task copyArtifactToDocker(type: Copy, dependsOn: dockerFile) {
    from "${buildDir}/libs/${project.name}-${project.version}-boot.jar"
    into 'build/docker'
}

// Build an image from the Dockerfile.
task dockerImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: copyArtifactToDocker) {
    inputDir = project.file('build/docker')
	if(!project.hasProperty("debug")) {
	    tag = "${dockerRepository}/sitewhere/${project.name}:${version}"
	} else {
	    tag = "${dockerRepository}/sitewhere/${project.name}:debug-${version}"		
	}
}

// Push image to remote repository.
task dockerPush(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage, dependsOn: dockerImage) {
    imageName = "${dockerRepository}/sitewhere/${project.name}"
    tag = "${version}"
}
