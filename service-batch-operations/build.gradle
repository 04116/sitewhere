description = 'SiteWhere Batch Operations Microservice'

dependencies {
	compile project(':sitewhere-core')
	compile project(':sitewhere-mongodb')
	compile project(':sitewhere-microservice')
	compile project(':sitewhere-grpc-batch-management')
	
	// Connectivity to other microservices.
	compile project(':sitewhere-grpc-device-management')
	compile project(':sitewhere-grpc-event-management')
	
	// HBase not currently supported.
	compileOnly project(':sitewhere-hbase')
}

license {
}

apply plugin: 'org.springframework.boot'
springBoot {
    mainClassName = 'com.sitewhere.batch.BatchOperationsApplication'
}

// Create a Dockerfile.
task dockerFile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile, dependsOn: bootJar) {
    destFile = project.file('build/docker/Dockerfile')
    from "${rootProject.ext['docker.base.image']}"
    maintainer "${rootProject.ext['docker.maintainer']}"
	
	// Expose ports.
	exposePort 8080
	
	// Copy Spring Boot jar.
	copyFile("${project.name}-${project.version}.jar", "/")

	// Run Java command to start application.
	defaultCommand 'java', '-Xmx768M', '-XX:+UnlockExperimentalVMOptions', '-XX:+UseCGroupMemoryLimitForHeap', '-jar', "/${project.name}-${project.version}.jar"
}

// Copy artifact to Docker input folder.
task copyArtifactToDocker(type: Copy, dependsOn: dockerFile) {
    from "${buildDir}/libs/${project.name}-${project.version}.jar"
    into 'build/docker'
}

// Build an image from the Dockerfile.
task dockerImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: copyArtifactToDocker) {
    inputDir = project.file('build/docker')
    tag = "${dockerRepository}/sitewhere/${project.name}:${version}"
}

// Push image to remote repository.
task dockerPush(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage, dependsOn: dockerImage) {
    imageName = "${dockerRepository}/sitewhere/${project.name}"
    tag = "${version}"
}
